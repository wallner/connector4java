apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

group 'org.osiam'
version '1.7-SNAPSHOT'

dependencies {
    compile(
            "org.osiam:scim-schema:1.4",
            "commons-io:commons-io:2.4",
            "joda-time:joda-time:2.8",
            "org.glassfish.jersey.connectors:jersey-apache-connector:2.17"
    )
    testCompile(
            "junit:junit:4.12",
            "nl.jqno.equalsverifier:equalsverifier:1.7.2",
            "org.hamcrest:hamcrest-library:1.3"
    )
}

jar {
    manifest {
        attributes  \
               'Implementation-Vendor': 'OSIAM team',  \
               'Implementation-Title': project.name,  \
               'Implementation-Version': project.version,  \
               'Built-JDK': System.getProperty('java.version'),  \
               'Built-By': System.getProperty('user.name')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

